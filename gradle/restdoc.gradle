configurations {
    jsondoclet
    asciidoctorExtensions
}

ext {
    snippetsDir = file('build/generated-snippets')
    javadocJsonDir = file("$buildDir/generated-javadoc-json")
}

dependencies {
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    implementation 'capital.scalable:spring-auto-restdocs-core:2.0.11'
    jsondoclet 'capital.scalable:spring-auto-restdocs-json-doclet-jdk9:2.0.11'

    testImplementation 'io.rest-assured:spring-web-test-client:4.4.0'
    testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'
}

task jsonDoclet(type: Javadoc, dependsOn: compileJava) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = javadocJsonDir
    options.docletpath = configurations.jsondoclet.files.asType(List)
    options.doclet = 'capital.scalable.restdocs.jsondoclet.ExtractDocumentationAsJsonDoclet'
    options.memberLevel = JavadocMemberLevel.PACKAGE
}

asciidoctor {
    configurations "asciidoctorExtensions"
    inputs.dir snippetsDir
    options backend: 'html', doctype: 'book'
    attributes 'source-highlighter': 'highlightjs'
    dependsOn check
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    systemProperty 'org.springframework.restdocs.javadocJsonDir', javadocJsonDir
    dependsOn jsonDoclet
}

bootJar {
    dependsOn asciidoctor
    // link to http(s)://ip:port/docs/api-guide.html
    from("${asciidoctor.outputDir}") {
        into 'BOOT-INF/classes/public/docs'
    }
}
